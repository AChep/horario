apply plugin: 'com.android.application'

import java.text.SimpleDateFormat

repositories {
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.artemchep.horario"
        minSdkVersion 16
        targetSdkVersion 25
        vectorDrawables.useSupportLibrary = true
        versionCode 10
        versionName "0.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    def releaseProps = new Properties()
    def releaseKeyFilename;
    def releasePropsFilename;
    if (file("release.properties").exists()) {
        releaseKeyFilename = "release-key.keystore";
        releasePropsFilename = "release.properties";
    } else {
        releaseKeyFilename = "dev-key.keystore";
        releasePropsFilename = "dev.properties";
    }
    file(releasePropsFilename).withInputStream { releaseProps.load(it) }
    String key = releaseProps['key_google_play_public'];

    Date date = Calendar.getInstance().getTime();
    String time = new SimpleDateFormat("EEE MMMM dd HH:mm:ss zzz yyyy", Locale.ENGLISH).format(date);
    String timeYear = new SimpleDateFormat("yyyy", Locale.ENGLISH).format(date);

    //-- SIGNING CONFIG -------------------------------------------------------

    signingConfigs {
        release {
            storeFile file(releaseKeyFilename)
            keyAlias releaseProps['key_alias']
            keyPassword releaseProps['password_store']
            storePassword releaseProps['password_key']
        }
    }

    //-- BUILD TYPES ----------------------------------------------------------

    buildTypes {
        debug {
            buildConfigField "boolean", "MY_DEBUG", "true"
            signingConfig signingConfigs.release
        }
        release {
            // Proguard
            zipAlignEnabled true
            minifyEnabled true
            proguardFile 'proguard-app.pro'
            proguardFile 'proguard-facebook.pro'
            proguardFile 'proguard-leakcanary.pro'
            proguardFile 'proguard-google-play-services.pro'
            proguardFile 'proguard-support-v7-cardview.pro'
            proguardFile 'proguard-support-v7-appcompat.pro'
            proguardFile 'proguard-support-design.pro'
            proguardFile 'proguard-chart.pro'
            proguardFile 'proguard-flowlayout.pro'
            proguardFile 'proguard-firebase-db.pro'

            // Sign
            signingConfig signingConfigs.release

            buildConfigField "boolean", "MY_DEBUG", "false"
        }
        beta {
            versionNameSuffix '-beta'
            initWith release
        }

        // Add config fields to each of build types.
        buildTypes.each {
            it.buildConfigField "String", "MY_TIME", "\"" + time + "\""
            it.buildConfigField "String", "MY_TIME_YEAR", "\"" + timeYear + "\""
            it.buildConfigField "String", "MY_GOOGLE_PLAY_PUBLIC_KEY", "\"" + key + "\""
        }
    }
}

ext {
    supportLibVersion = "25.3.1"
    firebaseLibVersion = "10.2.1"
    leakcanaryLibVersion = "1.5"
    materialDialogsLibVersion = "0.9.2.3"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        // use version of jsr305 provided by Checkout
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    testCompile 'junit:junit:4.12'
    compile 'com.github.GrenderG:Toasty:1.1.4'
    compile 'com.yarolegovich:discrete-scrollview:1.1.3'
    compile 'com.afollestad:material-cab:0.1.12'
    compile "com.afollestad.material-dialogs:core:${materialDialogsLibVersion}"
    compile "com.afollestad.material-dialogs:commons:${materialDialogsLibVersion}"
    compile 'uk.co.samuelwall:material-tap-target-prompt:1.10.0'
    compile 'com.ogaclejapan.smarttablayout:utils-v4:1.6.1@aar'
    compile 'com.ogaclejapan.smarttablayout:library:1.6.1@aar'
    compile 'com.bartoszlipinski:recyclerviewheader2:2.0.1'
    compile 'net.opacapp:multiline-collapsingtoolbar:1.3.1'
    compile 'jp.wasabeef:recyclerview-animators:2.2.5'
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.2'
    compile 'jp.wasabeef:richeditor-android:1.2.2'
    compile 'org.apmem.tools:layouts:1.10@aar'
    compile 'me.grantland:autofittextview:0.2.1'
    compile 'com.thebluealliance:spectrum:0.7.1'
    compile 'com.shawnlin:number-picker:2.4.1'
    compile 'com.github.clans:fab:1.6.4'

    // Facebook
    compile 'com.facebook.android:facebook-android-sdk:4.19.0'

    // Support Library
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"

    // Leak Canary
    debugCompile "com.squareup.leakcanary:leakcanary-android:${leakcanaryLibVersion}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanaryLibVersion}"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanaryLibVersion}"

    // Firebase
    compile "com.google.firebase:firebase-core:${firebaseLibVersion}"
    compile "com.google.firebase:firebase-auth:${firebaseLibVersion}"
    compile "com.google.firebase:firebase-crash:${firebaseLibVersion}"
    compile "com.google.firebase:firebase-config:${firebaseLibVersion}"
    compile "com.google.firebase:firebase-database:${firebaseLibVersion}"
    compile "com.google.android.gms:play-services-auth:${firebaseLibVersion}"

    compile project(":basic")
}

apply plugin: 'com.google.gms.google-services'
