apply plugin: 'com.android.application'

import java.text.SimpleDateFormat

repositories {
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.artemchep.horario"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    def releaseProps = new Properties()
    file("release.properties").withInputStream { releaseProps.load(it) }
    String key = releaseProps['key_google_play_public'];

    Date date = Calendar.getInstance().getTime();
    String time = new SimpleDateFormat("EEE MMMM dd HH:mm:ss zzz yyyy", Locale.ENGLISH).format(date);
    String timeYear = new SimpleDateFormat("yyyy", Locale.ENGLISH).format(date);

    //-- SIGNING CONFIG -------------------------------------------------------

    signingConfigs {
        release {
            storeFile file("release-key.keystore")
            keyAlias releaseProps['key_alias']
            keyPassword releaseProps['password_store']
            storePassword releaseProps['password_key']
        }
    }

    //-- BUILD TYPES ----------------------------------------------------------

    buildTypes {
        debug {
            buildConfigField "boolean", "MY_DEBUG", "true"
            signingConfig signingConfigs.release
        }
        release {
            // Proguard
            zipAlignEnabled true
            minifyEnabled true
            proguardFile 'proguard-app.pro'

            // Sign
            signingConfig signingConfigs.release

            buildConfigField "boolean", "MY_DEBUG", "false"
        }
        beta {
            versionNameSuffix '-beta'
            initWith release
        }

        // Add config fields to each of build types.
        buildTypes.each {
            it.buildConfigField "String", "MY_TIME", "\"" + time + "\""
            it.buildConfigField "String", "MY_TIME_YEAR", "\"" + timeYear + "\""
            it.buildConfigField "String", "MY_GOOGLE_PLAY_PUBLIC_KEY", "\"" + key + "\""
        }
    }
}

ext {
    supportLibVersion = "25.3.1"
    firebaseLibVersion = "10.2.1"
    leakcanaryLibVersion = "1.5"
    materialDialogsLibVersion = "0.9.2.3"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        // use version of jsr305 provided by Checkout
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.facebook.android:facebook-android-sdk:4.19.0'
    testCompile 'junit:junit:4.12'
    compile 'com.github.GrenderG:Toasty:1.1.4'
    compile 'com.afollestad:material-cab:0.1.12'
    compile "com.afollestad.material-dialogs:core:${materialDialogsLibVersion}"
    compile "com.afollestad.material-dialogs:commons:${materialDialogsLibVersion}"
    compile('com.mikepenz:materialdrawer:5.8.1@aar') {
        transitive = true
    }

    // Leak Canary
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"

    // Leak Canary
    debugCompile "com.squareup.leakcanary:leakcanary-android:${leakcanaryLibVersion}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanaryLibVersion}"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanaryLibVersion}"

    // Firebase
    compile "com.google.firebase:firebase-core:${firebaseLibVersion}"
    compile "com.google.firebase:firebase-auth:${firebaseLibVersion}"
    compile "com.google.firebase:firebase-crash:${firebaseLibVersion}"
    compile "com.google.firebase:firebase-config:${firebaseLibVersion}"
    compile "com.google.firebase:firebase-database:${firebaseLibVersion}"
    compile "com.google.android.gms:play-services-auth:${firebaseLibVersion}"

    compile project(":basic")
}

apply plugin: 'com.google.gms.google-services'
